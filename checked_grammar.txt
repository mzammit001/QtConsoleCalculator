START -> STMT.

OP_COMMA -> 'comma'.
OP_ASSIGN -> 'eq'.
OPEN_PAREN -> '('.
CLOSE_PAREN -> ')'.
OPEN_SET -> 'open_set'.
CLOSE_SET -> 'close_set'.
CMD_LOAD -> 'load'.
CMD_SAVE -> 'save'.
CMD_DEF -> 'def'.
CMD_CLEAR -> 'clear'.
CMD_VARS -> 'vars'.
CMD_FUNCS -> 'funcs'.
PREFIX_HEX -> '0x'.
PREFIX_BIN -> '0b'.
PREFIX_OCT -> '0o'.

UNARY_EXPRESSION -> UNARY_OP_PRCD_1.
UNARY_OP_PRCD_1 -> 'plus' EXPRESSION | 'minus' EXPRESSION | 'not' EXPRESSION.

BINARY_EXPRESSION -> BINARY_EXPR_PRCD_7.

BINARY_EXPR_PRCD_7 -> BINARY_EXPR_PRCD_6 BINARY_OP_PRCD_7.
BINARY_OP_PRCD_7 -> 'or' BINARY_EXPR_PRCD_6 BINARY_OP_PRCD_7 |.

BINARY_EXPR_PRCD_6 -> BINARY_EXPR_PRCD_5 BINARY_OP_PRCD_6.
BINARY_OP_PRCD_6 -> 'xor' BINARY_EXPR_PRCD_5 BINARY_OP_PRCD_6 |.

BINARY_EXPR_PRCD_5 -> BINARY_EXPR_PRCD_4 BINARY_OP_PRCD_5.
BINARY_OP_PRCD_5 -> 'and' BINARY_EXPR_PRCD_4 BINARY_OP_PRCD_5 |.

BINARY_EXPR_PRCD_4 -> BINARY_EXPR_PRCD_3 BINARY_OP_PRCD_4.
BINARY_OP_PRCD_4 -> 'lsh' BINARY_EXPR_PRCD_3 BINARY_OP_PRCD_4 | 'rsh' BINARY_EXPR_PRCD_3 BINARY_OP_PRCD_4 |.

BINARY_EXPR_PRCD_3 -> BINARY_EXPR_PRCD_2 BINARY_OP_PRCD_3.
BINARY_OP_PRCD_3 -> 'plus' BINARY_EXPR_PRCD_2 BINARY_OP_PRCD_3 | 'minus' BINARY_EXPR_PRCD_2 BINARY_OP_PRCD_3 |.

BINARY_EXPR_PRCD_2 -> BINARY_EXPR_PRCD_1 BINARY_OP_PRCD_2.
BINARY_OP_PRCD_2 -> 'mul' BINARY_OP_PRCD_1 BINARY_OP_PRCD_2 | 'div' BINARY_OP_PRCD_1 BINARY_OP_PRCD_2 | 'mod' BINARY_OP_PRCD_1 BINARY_OP_PRCD_2 |.

BINARY_EXPR_PRCD_1 -> EXPRESSION BINARY_OP_PRCD_1.
BINARY_OP_PRCD_1 -> 'pow' EXPRESSION BINARY_OP_PRCD_1 |.

EXPRESSION -> GROUPING | LITERAL | UNARY_EXPRESSION.
GROUPING -> OPEN_PAREN BINARY_EXPRESSION CLOSE_PAREN.
LITERAL -> NUMBER_TYPE | IDENTIFIER_TYPE.
IDENTIFIER_TYPE -> STR_TERM TERM FUNCTION_IDENTIFIER.

NUMBER_DIGIT -> '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9'.
NUMBER_DIGITS -> NUMBER_DIGIT NUMBER_DIGITS |.
HEX_DIGIT -> NUMBER_DIGIT | 'a' | 'b' | 'c' | 'd' | 'e' | 'f'.
HEX_DIGITS -> HEX_DIGIT HEX_DIGITS |.
BIN_DIGIT -> '0' | '1'.
BIN_DIGITS -> BIN_DIGIT BIN_DIGITS |.
OCT_DIGIT -> '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7'.
OCT_DIGITS -> OCT_DIGIT OCT_DIGITS |.
DEC_NUMBER -> NUMBER_DIGIT NUMBER_DIGITS.
HEX_NUMBER -> PREFIX_HEX HEX_DIGIT HEX_DIGITS.
BIN_NUMBER -> PREFIX_BIN BIN_DIGIT BIN_DIGITS.
OCT_NUMBER -> PREFIX_OCT OCT_DIGIT OCT_DIGITS.
FLOAT_NUMBER -> DEC_NUMBER FLOAT_NUMBER_OPT.
FLOAT_NUMBER_OPT -> 'dot' DEC_NUMBER |.
NUMBER_TYPE -> FLOAT_NUMBER | HEX_NUMBER | BIN_NUMBER | OCT_NUMBER.

STR_TERM -> 'a' | 'b' | 'c' | 'd' | 'e' | 'f' | 'g' | 'h' | 'i' | 'j' | 'k' | 'l' | 'm' | 'n' | 'o' | 'p' | 'q' | 'r' | 's' | 't' | 'u' | 'v' | 'w' | 'x' | 'y' | 'z' | '_'.
TERM -> STR_TERM TERM | NUMBER_DIGIT TERM |.
IDENTIFIER -> STR_TERM TERM.
FUNCTION_PARAM -> BINARY_EXPRESSION.
FUNCTION_PARAM_OPT -> OP_COMMA BINARY_EXPRESSION.
FUNCTION_PARAM_OPT_LIST -> FUNCTION_PARAM_OPT FUNCTION_PARAM_OPT_LIST |.
FUNCTION_IDENTIFIER -> OPEN_PAREN FUNCTION_IDENTIFIER_ARGS |.
FUNCTION_IDENTIFIER_ARGS -> CLOSE_PAREN | FUNCTION_PARAM FUNCTION_PARAM_OPT_LIST CLOSE_PAREN.

STMT -> BINARY_EXPRESSION | DEFINE_STMT | LISTVARS_STMT | LISTFUNC_STMT | SAVE_STMT | LOAD_STMT | CLEAR_STMT | SET_EXPR.
DEFINE_STMT -> CMD_DEF ASSIGNMENT_STMT.
LISTVARS_STMT -> CMD_VARS.
LISTFUNC_STMT -> CMD_FUNCS.
SAVE_STMT -> CMD_SAVE.
LOAD_STMT -> CMD_LOAD.
CLEAR_STMT -> CMD_CLEAR.
ASSIGNMENT_STMT -> IDENTIFIER ASSIGNMENT_TYPE.
ASSIGNMENT_TYPE -> FN_STMT_PARAM_LIST OP_ASSIGN BINARY_EXPRESSION | OP_ASSIGN BINARY_EXPRESSION.

FN_STMT_PARAM -> IDENTIFIER.
FN_STMT_PARAM_OPT -> OP_COMMA FN_STMT_PARAM.
FN_STMT_PARAM_OPT_LIST -> FN_STMT_PARAM_OPT FN_STMT_PARAM_OPT_LIST |.
FN_STMT_PARAM_LIST -> OPEN_PAREN FN_STMT_PARAM_LIST_ARGS.
FN_STMT_PARAM_LIST_ARGS -> FN_STMT_PARAM FN_STMT_PARAM_OPT_LIST CLOSE_PAREN | CLOSE_PAREN.

NUMBER_SET -> OPEN_SET NUMBER_SET_PARAM_LIST CLOSE_SET.
NUMBER_SET_PARAM -> NUMBER_TYPE.
NUMBER_SET_PARAM_OPT -> OP_COMMA NUMBER_SET_PARAM.
NUMBER_SET_PARAM_OPT_LIST -> NUMBER_SET_PARAM_OPT NUMBER_SET_PARAM_OPT_LIST |.
NUMBER_SET_PARAM_LIST -> NUMBER_SET_PARAM NUMBER_SET_PARAM_OPT_LIST.

SET_OP -> 'comp' | 'plus' | 'minus'.
SET_EXPR -> NUMBER_SET SET_OP NUMBER_SET.

