START -> STMT.

COMMA -> 'comma'.
EQUALS -> 'eq'.
OPEN_PARENTHESIS -> 'open_paren'.
CLOSE_PARENTHESIS -> 'close_paren'.
OPEN_SET -> 'open_set'.
CLOSE_SET -> 'close_set'.
KW_LOAD -> 'load'.
KW_SAVE -> 'save'.
KW_DEFINE -> 'def'.
KW_CLEAR -> 'clear'.
KW_VARS -> 'vars'.
KW_FUNC -> 'funcs'.
OP_PLUS -> 'plus'.
OP_MINUS -> 'minus'.
OP_NOT -> 'not'.
OP_OR -> 'or'.
OP_XOR -> 'xor'.
OP_AND -> 'and'.
OP_LSHIFT -> 'lsh'.
OP_RSHIFT -> 'rsh'.
OP_POW -> 'pow'.
OP_MULTIPLY -> 'mul'.
OP_DIVIDE -> 'div'.
OP_MODULUS -> 'mod'.
OP_COMPLEMENT -> 'comp'.

UNARY_EXPRESSION -> UNARY_OP_PRCD_1.
BINARY_EXPRESSION -> BINARY_EXPR_PRCD_7.

UNARY_OP_PRCD_1 -> OP_PLUS EXPRESSION | OP_MINUS EXPRESSION | OP_NOT EXPRESSION.

BINARY_EXPR_PRCD_7 -> BINARY_EXPR_PRCD_6 BINARY_OP_PRCD_7.
BINARY_OP_PRCD_7 -> OP_OR BINARY_EXPR_PRCD_6 BINARY_OP_PRCD_7 |.
BINARY_EXPR_PRCD_6 -> BINARY_EXPR_PRCD_5 BINARY_OP_PRCD_6.
BINARY_OP_PRCD_6 -> OP_XOR BINARY_EXPR_PRCD_5 BINARY_OP_PRCD_6 |.
BINARY_EXPR_PRCD_5 -> BINARY_EXPR_PRCD_4 BINARY_OP_PRCD_5.
BINARY_OP_PRCD_5 -> OP_AND BINARY_EXPR_PRCD_4 BINARY_OP_PRCD_5 |.
BINARY_EXPR_PRCD_4 -> BINARY_EXPR_PRCD_3 BINARY_OP_PRCD_4.
BINARY_OP_PRCD_4 -> OP_LSHIFT BINARY_EXPR_PRCD_3 BINARY_OP_PRCD_4 | OP_RSHIFT BINARY_EXPR_PRCD_3 BINARY_OP_PRCD_4 |.
BINARY_EXPR_PRCD_3 -> BINARY_EXPR_PRCD_2 BINARY_OP_PRCD_3.
BINARY_OP_PRCD_3 -> OP_PLUS BINARY_EXPR_PRCD_2 BINARY_OP_PRCD_3 | OP_MINUS BINARY_EXPR_PRCD_2 BINARY_OP_PRCD_3 |.
BINARY_EXPR_PRCD_2 -> BINARY_EXPR_PRCD_1 BINARY_OP_PRCD_2.
BINARY_OP_PRCD_2 -> OP_MULTIPLY BINARY_EXPR_PRCD_1 BINARY_OP_PRCD_2 | OP_DIVIDE BINARY_EXPR_PRCD_1 BINARY_OP_PRCD_2 | OP_MODULUS BINARY_EXPR_PRCD_1 BINARY_OP_PRCD_2 |.
BINARY_EXPR_PRCD_1 -> EXPRESSION BINARY_OP_PRCD_1.
BINARY_OP_PRCD_1 -> OP_POW EXPRESSION BINARY_OP_PRCD_1 |.
EXPRESSION -> GROUPING | VALUE | UNARY_EXPRESSION.
GROUPING -> OPEN_PARENTHESIS BINARY_EXPRESSION CLOSE_PARENTHESIS.
VALUE -> NUMBER_TYPE | IDENTIFIER_TYPE.
IDENTIFIER_TYPE -> NAME FUNCTION_IDENTIFIER.

NUMBER_TYPE -> 'LITERAL'.
NAME -> 'IDENTIFIER'.

FUNCTION_PARAM -> BINARY_EXPRESSION.
FUNCTION_PARAM_OPT -> COMMA BINARY_EXPRESSION.
FUNCTION_PARAM_OPT_LIST -> FUNCTION_PARAM_OPT FUNCTION_PARAM_OPT_LIST |.
FUNCTION_IDENTIFIER -> OPEN_PARENTHESIS FUNCTION_IDENTIFIER_ARGS |.
FUNCTION_IDENTIFIER_ARGS -> CLOSE_PARENTHESIS | FUNCTION_PARAM FUNCTION_PARAM_OPT_LIST CLOSE_PARENTHESIS.

STMT -> BINARY_EXPRESSION | DEFINE_STMT | LISTVARS_STMT | LISTFUNC_STMT | SAVE_STMT | LOAD_STMT | CLEAR_STMT | SET_EXPR.
DEFINE_STMT -> KW_DEFINE ASSIGNMENT_STMT.
LISTVARS_STMT -> KW_VARS.
LISTFUNC_STMT -> KW_FUNC.
SAVE_STMT -> KW_SAVE.
LOAD_STMT -> KW_LOAD.
CLEAR_STMT -> KW_CLEAR.
ASSIGNMENT_STMT -> NAME ASSIGNMENT_TYPE.
ASSIGNMENT_TYPE -> FN_STMT_PARAM_LIST EQUALS BINARY_EXPRESSION | EQUALS BINARY_EXPRESSION.

FN_STMT_PARAM -> NAME.
FN_STMT_PARAM_OPT -> COMMA FN_STMT_PARAM.
FN_STMT_PARAM_OPT_LIST -> FN_STMT_PARAM_OPT FN_STMT_PARAM_OPT_LIST |.
FN_STMT_PARAM_LIST -> OPEN_PARENTHESIS FN_STMT_PARAM_LIST_ARGS.
FN_STMT_PARAM_LIST_ARGS -> FN_STMT_PARAM FN_STMT_PARAM_OPT_LIST CLOSE_PARENTHESIS | CLOSE_PARENTHESIS.

NUMBER_SET -> OPEN_SET NUMBER_SET_PARAM_LIST CLOSE_SET.
NUMBER_SET_PARAM -> NUMBER_TYPE.
NUMBER_SET_PARAM_OPT -> COMMA NUMBER_SET_PARAM.
NUMBER_SET_PARAM_OPT_LIST -> NUMBER_SET_PARAM_OPT NUMBER_SET_PARAM_OPT_LIST |.
NUMBER_SET_PARAM_LIST -> NUMBER_SET_PARAM NUMBER_SET_PARAM_OPT_LIST.

SET_OP -> OP_COMPLEMENT | OP_PLUS | OP_MINUS.
SET_EXPR -> NUMBER_SET SET_OP NUMBER_SET.
